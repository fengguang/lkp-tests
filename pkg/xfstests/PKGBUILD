# Maintainer: Masami Ichikawae <masami256@gmail.com>
# vim:set ts=2 sw=2 et filetype=sh:
pkgname=xfstests-dev
pkgver=v1.1.0.r1626.g52f9ebbd
pkgrel=1
pkgdesc="File system QA suite"
arch=('i686' 'x86_64')
url="http://oss.sgi.com/projects/xfs"
license=('GPL')
install=xfstests.install
depends=('xfsprogs' 'openssl' 'libaio')
makedepends=('git')
source=("$pkgname"::'git://git.kernel.org/pub/scm/fs/xfs/xfstests-dev.git')
md5sums=('SKIP') # skip git source checksum

pkgver() {
    cd "$srcdir/$pkgname"
    git describe --long | sed -E 's/([^-]*-g)/r\1/;s/-/./g'
}

build() {
    cd "$srcdir/$pkgname"
    make configure
    ./configure
    make
}

package() {
    cd "$srcdir/$pkgname"
    make DESTDIR="$pkgdir/" install
}
