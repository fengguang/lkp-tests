# $Id$
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=piglit
_gitname=piglit
pkgver=r9427.7352f4a98
pkgrel=1
pkgdesc="Open-source test suite for OpenGL implementations"
arch=('i686' 'x86_64')
url="http://piglit.freedesktop.org/"
license=('MIT' 'GPL')
depends=('waffle' 'python2-mako' 'python2-numpy' 'glu')
makedepends=('git' 'cmake' 'glproto')
conflicts=('piglit')
provides=('piglit')
options=('!emptydirs')
source=('git://anongit.freedesktop.org/piglit')
md5sums=('SKIP')

pkgver() {
  cd $_gitname
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd $_gitname

  find . -type f -name '*.py' | xargs sed -i 's@^#.*python$@#!/usr/bin/python2@'
}

build() {
  cd $_gitname

  [[ -d build ]] || mkdir build
  cd build

  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
           -DCMAKE_SKIP_RPATH=true \
           -DPIGLIT_BUILD_GLES1_TESTS=true \
           -DPIGLIT_BUILD_GLES2_TESTS=true
  make
}

package() {
  cd $_gitname/build
  make DESTDIR="$pkgdir" install

  for sofile in "$pkgdir"/usr/lib/piglit/lib/*.so; do
    sofile=$(basename $sofile)
    ln -s piglit/lib/$sofile "$pkgdir"/usr/lib/$sofile
  done

  install -Dm644 ../COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
